apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'antlr'
apply plugin:'application'

group = 'com.github.peterzeller'
version = '0.3.3'

description = """Abstract Syntax Generator"""


String genDir = "$projectDir/src-generated"

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(25))
    }
}
tasks.withType(JavaCompile).configureEach { options.release = 25 }

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir genDir
        }
    }
}

repositories {
    mavenCentral()
}
dependencies {
    antlr "org.antlr:antlr4:4.13.1"
    implementation 'com.google.guava:guava:33.5.0-jre'
}


generateGrammarSource {
    outputDirectory = file(genDir)
}

clean.doFirst {
    delete genDir
}


tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    dependsOn 'generateGrammarSource'
    archiveClassifier.set('javadoc')
    from 'build/docs/javadoc'
}

tasks.register('sourcesJar', Jar) {
    dependsOn 'generateGrammarSource'
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'com.github.peterzeller'
            artifactId = 'abstractsyntaxgen'
            version = '0.3.3'
        }
    }
    repositories {
        mavenLocal()
    }
}

// Ensure artifacts are published to mavenLocal() during the build
build.dependsOn publishToMavenLocal

